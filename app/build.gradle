apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def props = new Properties()
props.load new FileInputStream(new File('secrets.properties'))

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"

    defaultConfig {

        applicationId "com.efemoney.forecast"

        minSdkVersion 16
        targetSdkVersion 25

        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        buildConfigField 'String', 'OWM_API_KEY', "\"${props['OWM_API_KEY']}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {

    supportVersion = '25.4.0'
    playServicesVersion = '11.0.1'
    gsonVersion = '2.8.1'
    daggerVersion = '2.11'
    okHttpVersion = '3.8.0'
    retrofitVersion = '2.3.0'
    rxJavaVersion = '2.1.0'
    rxAndroidVersion = '2.0.1'
}

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"

    implementation 'com.github.bumptech.glide:glide:3.8.0'

    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"

    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    implementation "com.google.android.gms:play-services-location:$playServicesVersion"

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"

    compileOnly 'javax.annotation:jsr250-api:1.0' // Contains annotations used by Dagger

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.9'

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
